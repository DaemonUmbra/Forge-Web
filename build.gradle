plugins {
    id "io.freefair.jsass-base" version "1.0.0"
    id "com.eriwen.gradle.js" version "2.14.1"
}

task css(type: io.freefair.gradle.plugins.jsass.SassCompile) {
    sassPath = "${projectDir}/sass"
    cssPath = "${buildDir}/css"
    sourceMapEnabled = false
    outputStyle = io.bit3.jsass.OutputStyle.COMPRESSED
}

def createJsTask(name, unique, includeShared = true) {
    def combineTask = null
    if (includeShared) {
        combineTask = tasks.create("combine${name.capitalize()}Js", com.eriwen.gradle.js.tasks.CombineJsTask) {
            source = fileTree("${projectDir}/js/shared").include('**/*.js').filter { it.isFile() }.files.path + unique
            dest = file("${buildDir}/js/${name}.js")
        }
    }
    def minifyTask = tasks.create("minify${name.capitalize()}Js", com.eriwen.gradle.js.tasks.MinifyJsTask) {
        source = includeShared ? combineTask : unique[0]
        dest = file("${buildDir}/js/${name}.js")
    }
}

createJsTask('files', ["${projectDir}/js/files.js"])
createJsTask('docs', ["${projectDir}/js/docs.js"])
createJsTask('themeSwitch', ["${projectDir}/js/theme-switch.js"], false)

task bundleFiles(type: Zip) {
    baseName = 'files-bundle'
    destinationDir = file("${buildDir}/distributions")
    from(minifyFilesJs) {
        into 'static/js'
        rename ~/files/, 'merged'
    }
    from(css) {
        include 'website_*.css'
        into 'static/css'
        rename ~/website_([A-Za-z]+)/, 'styles_$1'
    }
    from("${projectDir}/templates") {
        include '**/*.html'
        into 'templates'
    }
    from("${projectDir}/docs") {
        include '**/*.html'
        into 'static'
    }
}
